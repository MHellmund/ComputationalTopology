#####line break required to define a new function#####
##### Setting the parameters for D=6#####
maxobjects 30000000
#setdefault D3A1
setdefault Lie_group(4, 3)*Lie_group(1, 1)
rank=4
NN=2
s=X[0,0,1,1]
v=X[1,0,0,0]
#dim([0,0,1,1],D3A1)=8
#dim(X[0,0,1],D3)=4,dim(X[0,0,1]+X[0,1,0],D3)=8
#dim(X[1,0,0],D3)=6
##### calculate content of superfields #####
r(int m,n)=
{
if n<0 then poly_null(rank) else
        if m<0 then poly_null(rank) else
                if n==0 then sym_tensor(m,s) else
                        tensor(sym_tensor(m,s),alt_tensor(n,v))
                fi;
        fi;
fi;
}
##### set negative multiplicities to zero #####
pos_pol(pol p)=
{loc q=p;
for i=1 to length(p) do
        if coef(p,i)<0 then q=q-p[i];
        fi;
od;
q}
##### subtract multiplicities propagating from the left # #####
left(int m,n)=
{
loc t=poly_null(rank);
loc n0=0;
if m-2<0 then
        r(m,n);
else
        k=m+2*n;
        if k<4*(rank-NN/2) then n0=k/2 else
                if k>=4*(rank-NN/2) then  n0=2*(rank-NN/2)
                fi;
        fi;
        t=r(k-2*n0,n0);
        for i=n0-1 downto n do
                t=pos_pol(r(k-2*i,i)-t);
        od;
        t;
fi
}
##### subtract multiplicities propagating to the right #####
right(int m,n)=
{
loc t=poly_null(rank);
if n-1<0 then
        r(m,n);
else
        k=m+2*n;
        t=r(k,0);
        for i=1 to n do
                t=pos_pol(r(k-2*i,i)-t);
        od;
        t;
# r(m,n)-Ker(m,n);
fi
}
##### calculate candidate cohomologies per differentials#####
h(int m, n)=pos_pol(r(m,n)-left(m-2,n+1)-right(m+2,n-1))
#####calculate candidate cohomologies #####
coh(int k)=
{
loc t=poly_null(rank);
for i=0 to k/2 do
        t=t+h(k-2*i, i);
        od;
        t;
}
#####Prove the maximal propagation#####
###different variable name kk########
#sed -e :a -e '$!N;s/\n[[:space:]]*\\//g;s/\\\n[[:space:]]*//g;s/==>+/==>/g;ta' -e 'P;D' result.txt >result_formated.txt#
#sed -e '{s/\ //g; s/\\//g;}' result.txt |awk '{printf"%s",$0}' |sed -e 's/==>+/\n==>/g;s/:/:\n/g;'#
maxprop(int kk)=
{
iMax=kk/2;
if kk>4*(rank-NN/2) then iMax=2*(rank-NN/2); fi;
print("k="+kk+":");
loc mincoef=0;
loc rB=poly_null(rank);
loc rH=poly_null(rank);
loc HH=poly_null(rank);
for i=iMax downto 0 do
      SxH=tensor(s,h(kk-1-2*i, i));
      for j=1 to length(h(kk-1-2*i, i)) do
         print("Sx\");
         print(h(kk-1-2*i,i)[j]);
         print("\_H_"+(i)+"=\");
         print(SxH[j]);
      od;
#      B_lowk=pos_pol(r(kk-1-2*i,i)-h(kk-1-2*i,i)-r(kk-1-2*(i-1),i-1));
      B_lowk=right(kk-1-2*(i-1),i-1)-h(kk-1-2*(i-1),i-1);
      SxB=tensor(s,B_lowk);
      for j=1 to length(B_lowk) do
         SB=tensor(s,B_lowk[j]);
         print("Sx\");
         print(B_lowk[j]);
         print("\_B_"+(i)+"=\");
         print(SB);
      od;
od;
for i=iMax downto 0 do
#      SxH=tensor(s,h(kk-1-2*i, i));
#      B_lowk=pos_pol(r(kk-1-2*i,i)-h(kk-1-2*i,i)-r(kk-1-2*(i-1),i-1));
###Assume maximal propagation for low k###
      B_lowk=right(kk-1-2*(i-1),i-1)-h(kk-1-2*(i-1),i-1);
      SxB=tensor(s,B_lowk);
     if i==iMax then print("0==>"); fi;
     print("==>");
      for j=1 to length(rB) do
        print("\+\");
        print(rB[j]);
        print("\_B_"+(i+1));
      od;
#      print("k="+kk);
#      print("B_lowk:");
#      print(B_lowk);
#      print("SxB:");
#      print(SxB);
###supersymmetry condition###
#   if kk<=4*(rank-NN/2) && i*2==kk then
#       rB=r(0,kk/2); 
#       rH=poly_null(rank);
#   else
      rHrB=r(kk-2*i,i)-rB;
      rB=poly_null(rank);
      for j=1 to length(rHrB) do
         if coef(rHrB,j)<=r(kk-2*(i-1),i-1)|expon(rHrB,j) then mincoef=coef(rHrB,j) else mincoef=r(kk-2*(i-1),i-1)|expon(rHrB,j); fi;
         if !kk<=4*(rank-NN/2) && !i*2==kk && mincoef>SxB|expon(rHrB,j) then mincoef=SxB|expon(rHrB,j); fi;
#           if mincoef>SxB|expon(rHrB,j) then mincoef=SxB|expon(rHrB,j); fi;
         rB=rB+mincoef X expon(rHrB,j);
      od;
      rH=rHrB-rB;
#   fi;
      HH=HH+rH;
      for j=1 to length(rB) do
         print("\+\");
         print(rB[j]);
         print("\_B_"+(i));
       od;
      for j=1 to length(rH) do
         print("\+\");
         print(rH[j]);
         print("\_H_"+(i));
       od;
  od;
print("==>0");
print("Cohomology:");
print(HH);
if HH==coh(kk) then 
   print("Bingle! Our cohomology formula is verified.");
else
   print("Problem here!");
fi;
}
###Sx[a,b,c,d,e], [a,b,c,d,e]x\wedge^nV###
#NO if else if fi, BUT if else fi, OR if else if fi;fi;#
xsv(int n; vec iv)={
	loc aRep=6*rank*all_one(rank);
	loc isv=poly_null(rank);
	if n==0 then isv=tensor(s, 1 X aRep) else
		if n>=1 then isv=tensor(1 X aRep, r(0,n)) fi;
		fi;
	loc lensv=length(isv);
	loc epsi=null(lensv, rank);
	loc sv=poly_null(rank);
	loc av=null(rank);
	loc avt="[";
	for i=1 to rank do
	  avt=avt+"a"+i+",";
	 od;
 	if n==0 then print("Sx"+avt+"]=") else
	if n>=1 then print(avt+"] x \wedge^"+(n)+"V="); fi;
#	if n==0 then print("Sx[a,b,c,d,e]=") else
#		if n>=1 then print("[a,b,c,d,e] x \wedge^"+(n)+"V="); fi;
	fi;
	for i=1 to lensv do
	    im=1;
	    avtx="X[";
	    for j=1 to rank do
	        epsi[i,j]=expon(isv,i)[j]-aRep[j];
	        if epsi[i,j]>=0 then
	           avtx=avtx+" a"+j+"+"+(epsi[i,j])+","
	          else avtx=avtx+" a"+j+(epsi[i,j])+","; 
	         fi;
	        av[j]=iv[j]+epsi[i,j];
	        if av[j]<0 then im=0; fi;
	     od;
	     print("+"+(coef(isv,i))+avtx+"]");
	    #print("+"+(coef(isv,i))+"X[a+"+(epsi[i,1])+",b+"+(epsi[i,2])+",c+"+(epsi[i,3])+",d+"+(epsi[i,4])+",e+"+(epsi[i,5])+"]");
	    if im==1 then
	      	sv=sv+coef(isv,i) X av;
	   fi;
	 od;
	 print("epsilon=");
	 print(epsi);
	 print(sv);
	 if n==0 then print("The result above differed with Sx"+(iv)+":");
	 else
	    print("The result above differed with "+(iv)+"x\wedge^"+n+"V:");
	 fi;
	 if n==0 then print(sv-tensor(s,1 X iv)) else
	 	if n>=1 then print(sv-tensor(1 X iv, r(0,n))) fi;
	 	fi;
}