#####line break required to define a new function#####
##### definitions for D=10 SYM #####
maxobjects 10000000
#setdefault D4T1 A1A1A1A1T1 A3A1A1
subg=A1A1A1A1T1
setdefault subg
rank=5
s=X[0,0,0,1,0]
v=X[1,0,0,0,0]
resmatrix=[[1,0,0,0,0],[1,2,2,1,1],[0,0,1,0,0],[0,0,1,1,1]]
#Cartan_type(resmatrix,D5)
resmatrix=res_mat(closure(resmatrix,D5),D5)
#resmatrix=res_mat(subg,D5)
s=branch(s,subg,resmatrix,D5)
v=branch(v,subg,resmatrix,D5)
##### calculate content of superfields #####
r(int m,n)=
{
if n<0 then poly_null(rank) else
	if m<0 then poly_null(rank) else
		if n==0 then sym_tensor(m,s) else
			tensor(sym_tensor(m,s),alt_tensor(n,v))
		fi;
	fi;
fi;
}
##### set negative multiplicities to zero #####
pos_pol(pol p)=
{loc q=p;
for i=1 to length(p) do
	if coef(p,i)<0 then q=q-p[i];
	fi;
od;
q}
##### subtract multiplicities from the left # #####
left(int m,n)=
{
loc t=poly_null(rank);
loc n0=0;
if m-2<0 then
	r(m,n);
else
	k=m+2*n;
        if k<4*rank then n0=k/2 else
        	if k>=4*rank then  n0=2*rank
        	fi;
        fi;
	t=r(k-2*n0,n0);
        for i=n0-1 downto n do
        	t=pos_pol(r(k-2*i,i)-t);
        od;
        t;	
fi
}
##### subtract multiplicities from the right #####
right(int m,n)=
{
loc t=poly_null(rank);
if n-1<0 then
	r(m,n);
else
	k=m+2*n;
	t=r(k,0);
        for i=1 to n do
        	t=pos_pol(r(k-2*i,i)-t);
        od;
        t;
# r(m,n)-Ker(m,n);
fi
}
##### calculate candidate cohomologies per differentials#####
h(int m, n)=pos_pol(r(m,n)-left(m-2,n+1)-right(m+2,n-1))
#####calculate candidate cohomologies #####
coh(int k)=
{
loc t=poly_null(rank);
for i=0 to k/2 do
	t=t+h(k-2*i, i);
	od;
	t;
}
###a conjectured cohomology formula for the case of SO(10) ###
coh10(int k)=
{
loc t=poly_null(rank);
if k==4 then t=X[0,0,0,0,0]+X[0,0,0,4,0]+X[0,0,0,1,1]
else if k<3 then t=X[0,0,0,k,0]
else if k<6 then t=X[0,0,0,k,0]+X[0,0,0,k-3,1]
else if k<8 then t=X[0,0,0,k,0]+X[0,0,0,k-3,1]+X[0,0,1,k-6,0]
else if k<10 then	t=X[0,0,0,k,0]+X[0,0,0,k-3,1]+X[0,0,1,k-6,0]+X[0,1,0,k-8,0]
else if k<12 then	t=X[0,0,0,k,0]+X[0,0,0,k-3,1]+X[0,0,1,k-6,0]+X[0,1,0,k-8,0]+X[1,0,0,k-10,0]
else if k>=12 then t=X[0,0,0,k,0]+X[0,0,0,k-3,1]+X[0,0,1,k-6,0]+X[0,1,0,k-8,0]+X[1,0,0,k-10,0]+X[0,0,0,k-12,0]
fi;
fi;
fi;
fi;
fi;
fi;
fi;
t;
}
#####Check whether the cohomologies for the case of SO(10) are well contained in that of its subgroup #####
coh10subcheck(int k)=
{
print("Compare the cohomologies for the cases of SO(4)xSO(4)xSO(2) and SO(10):");
loc q=branch(coh10(k),subg,resmatrix,D5);
q-coh(k);
}

